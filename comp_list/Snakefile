configfile: "comps.yaml"

rule all:
    input:
        expand("cmp/{key}.pdf", key=config['comparisons'].keys()),
        "summary.pdf"

rule eset:
    input:
        rscript = "eset.r",
        config = "comps.yaml",
        infile = "../data/rnaseq.rds"
    output:
        outfile = "eset/{key}.rds"
    shell:
        "Rscript {input.rscript}"
            " --config {input.config}"
            " --key '{wildcards.key}'"
            " --infile '{input.infile}'"
            " --outfile '{output.outfile}'"

rule de:
    input:
        rscript = "deseq+sets.r",
        infile = "eset/{key}.rds"
    output:
        outfile = "cmp/{key}.xlsx"
    shell:
        "Rscript {input.rscript}"
            " --infile '{input.infile}'"
            " --outfile '{output.outfile}'"

rule plot:
    input:
        rscript = "plot.r",
        infile = "cmp/{key}.xlsx"
    output:
        plotfile = "cmp/{key}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile '{input.infile}'"
            " --plotfile '{output.plotfile}'"

rule summary:
    input:
        rscript = "summary.r",
        config = "comps.yaml",
        infiles = expand("cmp/{key}.xlsx", key=config['comparisons'].keys())
    params:
        qin = expand("'cmp/{key}.xlsx'", key=config['comparisons'].keys())
    output:
        outfile = "summary.rds"
        plotfile = "summary.pdf"
    shell:
        "Rscript {input.rscript}"
            " --config {input.config}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"
            " {params.qin}"
