configfile: "../config.yaml"

subworkflow genesets:
    workdir: "genesets"
    snakefile: "genesets/Snakefile"

rule all:
    input:
        "rnaseq.pdf", "tcga.rds",
        expand("gsva/{sets}.rds", sets=config['genesets'])

rule rnaseq:
    input:
        rscript = "rnaseq.r",
        samples = "rnaseq.tsv"
    output:
        outfile = "rnaseq.rds",
        plotfile = "rnaseq.pdf"
    shell:
        "Rscript {input.rscript}"
            " --samples {input.samples}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule gsva:
    input:
        rscript = "gsva.r",
        eset = "rnaseq.rds",
        setfile = genesets("human/{gset}.rds")
    output:
        outfile = "gsva/{gset}.rds"
    shell:
        "Rscript {input.rscript}"
            " --eset {input.eset}"
            " --setfile {input.setfile}"
            " --outfile {output.outfile}"

rule tcga:
    input:
        rscript = "tcga.r"
    output:
        outfile = "tcga.rds"
    shell:
        "Rscript {input.rscript}"
            " --outfile {output.outfile}"
