configfile: "../config.yaml"

subworkflow data:
    workdir: "../data"
    snakefile : "../data/Snakefile"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile : "../data/genesets/Snakefile"

rule all:
    input:
        expand("rev_effect-{time}h.pdf", time=['24', '48']),
        "rep_compare_plot.pdf", "inflamm_resp_overview.pdf"

rule deseq:
    input:
        rscript = "deseq.r",
        eset = data("rnaseq.rds"),
        setfiles = genesets(expand("human/{set}.rds", set=config["genesets"]))
    output:
        outfile = "deseq-{time}h.rds"
    shell:
        "Rscript {input.rscript}"
            " --eset {input.eset}"
            " --time {wildcards.time}"
            " --outfile {output.outfile}"
            " {input.setfiles}"

rule plot:
    input:
        rscript = "plot.r",
        infile = "deseq-{time}h.rds"
    output:
        plotfile = "rev_effect-{time}h.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --plotfile {output.plotfile}"

rule rep_compare:
    input:
        rscript = "rep_compare.r",
        eset = data("rnaseq.rds"),
        setfiles = genesets(expand("human/{gset}.rds",
            gset=["MSigDB_Hallmark_2020", "GO_Biological_Process_2021"]))
    output:
        outfile = "rep_compare-{time}h.rds"
    shell:
        "Rscript {input.rscript}"
            " --eset {input.eset}"
            " --time {wildcards.time}"
            " --outfile {output.outfile}"
            " {input.setfiles}"

rule rep_compare_plot:
    input:
        rscript = "{rscript}.r",
        short = "rep_compare-24h.rds",
        long = "rep_compare-48h.rds"
    output:
        plotfile = "{rscript}.pdf"
#    wildcard_constraints:
#        rscript = "(rep_compare_plot|inflamm_resp_overview)\\.r"
    shell:
        "Rscript {input.rscript}"
            " --short {input.short}"
            " --long {input.long}"
            " --plotfile {output.plotfile}"
